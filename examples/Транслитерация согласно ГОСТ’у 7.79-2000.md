# Транслитерация согласно ГОСТ’у 7.79-2000

```
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
//		Транслитерация согласно ГОСТ'у 7.79—2000 / ISO 9 
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
// Транслитерация с русского согласно ISO 9
// Функция возвращает транслит текстом соблюдая изначальный регистр.
// Параметры:
//	Текст	- [обязательный](строка) параметром передается текст на транслитерацию
//	Правило	- [необязательный](строка) правило CSV строкой с разделителями ":" и ";"
Функция TransliteracziyaISO9(Знач Текст, Знач Правило = "") Экспорт
	// Устанавливаю соответсвия между русскими символами и латиницей
	Если ПустаяСтрока(Правило) Тогда 
		Правило		= 
		"А:a;Б:b;В:v;Г:g;Д:d;Е:e;Ё:yo;Ж:zh;З:z;И:i;Й:j;К:k;
		|Л:l;М:m;Н:n;О:o;П:p;Р:r;С:s;Т:t;У:u;Ф:f;Х:x;Ц:cz;
		|Ч:ch;Ш:sh;Щ:shh;Ъ:``;Ы:y`;Ь:`;Э:e`;Ю:yu;Я:ya;’:';№:#";
	КонецЕсли;
	Правило		= СтрЗаменить(Правило, Символы.ПС, "");	
	Соответсвие	= Новый Соответствие; 
	Для Каждого Элт Из СтрРазделить(Правило, ";", Ложь) Цикл
		Соответсвие.Вставить(НРег(Лев(Элт, 1)), НРег(Сред(Элт, 3)));	
	КонецЦикла;
	Результат	= "";
	Правило		= Неопределено;
	
	// Перебираю посимвольно исходный текст
	Для Счетчик = 1 По СтрДлина(Текст) Цикл
		Буква	= Сред(Текст, Счетчик, 1);
		Если Соответсвие[НРег(Буква)] = Неопределено  Тогда
			// Оставлять нетронутыми исходные символы
			Буква	= Буква;	
		ИначеЕсли Буква = НРег(Буква) Тогда
			// Символы в нижнем регистре
			Буква	= Соответсвие[Буква];
		Иначе
			// Символы в верхнем регистре
			Буква	= ТРег(Соответсвие[НРег(Буква)]);
		КонецЕсли;		
		Результат	= Результат + Буква;
	КонецЦикла;	
	Возврат Результат;
КонецФункции // TransliteracziyaISO9()
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
```

