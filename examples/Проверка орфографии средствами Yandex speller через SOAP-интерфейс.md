# Проверка орфографии средствами Yandex speller через SOAP-интерфейс

Пример шаблона проверки и подключения:

```
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
//						Yandex speller 
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
// Проверка орфографии средствами Yandex speller через  SOAP-интерфейс
// https://tech.yandex.ru/speller/doc/dg/concepts/speller-overview-docpage/
//
// Функция возвращает:
//	Истина			- Текст не содержит ошибок или передана пустая строка
//	Ложь			- Текст содержит ошибки
//	Неопределено	- Проверка не удалась
//
// Параметры:
//	Текст - [обязательный](строка) параметром передается текст на проверку
Функция YandexSpeller(Знач Текст) Экспорт
	// Для проформы откидываю все лишнее.
	// И не мучаю веб сервис по напрасну.
	Если ТипЗнч(Текст) <> Тип("Строка") Тогда
		Возврат Неопределено;
	ИначеЕсли ПустаяСтрока(Текст) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Попытка
		// Подключение к Web сервису через SOAP-интерфейс
		Определения	= Новый WSОпределения("http://speller.yandex.net/services/spellservice?WSDL", , , Неопределено, 300);	
		Прокси		= Новый WSПрокси(Определения, "http://speller.yandex.net/services/spellservice", "SpellService", "SpellServiceSoap", Неопределено, 300);
		Данные		= Прокси.checkText(Текст);
		
		// Задаю соответсвие для расшифровки кодов ошибок
		КодыОшибок	= Новый Соответствие;
		КодыОшибок.Вставить(1, "Слова нет в словаре");
		КодыОшибок.Вставить(2, "Повтор слова");
		КодыОшибок.Вставить(3, "Неверное употребление прописных и строчных букв");
		КодыОшибок.Вставить(4, "Текст содержит слишком много ошибок");
		
		// Перебираю все ошибки
		ДЛЯ КАЖДОГО Ошибка ИЗ Данные.error ЦИКЛ
			Подсказки	= "";
			// Перебираю все доступные подсказки 
			// для уведомления пользователя о доступных вариантах
			Для Каждого Подсказка Из Ошибка.s Цикл
				Подсказки	= Подсказки + Подсказка + "; ";
			КонецЦикла;
			Подсказки	= Лев(Подсказки, СтрДлина(Подсказки)-2);
			
			// Непосредственно вывод по ошибке
			Сообщить(СтрШаблон("Ошибка в слове: ""%1"". %2. %3", 
						Ошибка.Word, 
						КодыОшибок.Получить(Ошибка.code),
						?(Ошибка.s.Количество(), Символы.ПС + "Возможно Вы хотели написать: " + Подсказки, "")));
		КОНЕЦЦИКЛА; // ДЛЯ КАЖДОГО Ошибка ИЗ Данные.error
			
		Возврат ?(Данные.error.Количество(), Ложь, Истина);			
	Исключение
		Сообщить("Не удалось проверить орфографию." + Символы.ПС + ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
КонецФункции // YandexSpeller()
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
```

