**ОТПРАВКА НА HTTP СЕРВИС** 

```
// Получим номенклатуру по коду GET запросом
Функция НоменклатураПоКодуGET(Код) Экспорт
    // Получаем текст корневой страницы через GET-запрос.
    Запрос        = Новый HTTPЗапрос("/hips/hs/Mobile/ItemsByCode/" + Код);
    Результат    = ДанныеДляПодключения().Получить(Запрос);
 
    Если Результат.КодСостояния <> 200 Тогда ВызватьИсключение "Ошибка подключения! " + Результат.ПолучитьТелоКакСтроку(); КонецЕсли; 
    // [200] - хороший код возврата
    // [400] - ошибка запроса
    
    // в теле результата запроса - json
    Ответ    = Результат.ПолучитьТелоКакСтроку();
    
    Если ПустаяСтрока(Ответ) Тогда ВызватьИсключение "Получена пустая строка!"; КонецЕсли;
    
    Возврат ДесериализоватьJSON(Ответ);    
КонецФункции // НоменклатураПоКодуGET()

// Создадим документ поступления или списания POST запросом
Функция СоздатьПоступлениеСписаниеPOST(Код, Количество, ПоступлениеСписание) Экспорт
    СтрокаJSON        = СериализоватьJSON(Новый Структура("Код,Количество", Код, Количество));
    HTTPСоединение    = ДанныеДляПодключения();
    HTTPЗапрос        = Новый HTTPЗапрос(СтрШаблон("/hips/hs/Mobile/%1/", ?(ПоступлениеСписание="Списание", "CreateConsumption", "CreateArrival")));
    HTTPЗапрос.Заголовки.Вставить("Content-type", "application/json"); 
    HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаJSON, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);

    Результат        = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос); 
    Если Результат.КодСостояния <> 200 Тогда 
        Сообщить("Ошибка подключения! " + Результат.ПолучитьТелоКакСтроку()); 
        Возврат Ложь; 
    КонецЕсли;    
    Возврат Истина;
КонецФункции // СоздатьПоступлениеСписаниеPOST()

Функция СериализоватьJSON(Данные)    
    ЗаписьJSON                                                = Новый ЗаписьJSON; 
    ЗаписьJSON.УстановитьСтроку();
    НастройкиСериализации                                    = Новый НастройкиСериализацииJSON();
    НастройкиСериализации.СериализовыватьМассивыКакОбъекты    = Ложь;
    НастройкиСериализации.ФорматСериализацииДаты            = ФорматДатыJSON.ISO;
    НастройкиСериализации.ВариантЗаписиДаты                    = ВариантЗаписиДатыJSON.УниверсальнаяДата;
    ЗаписатьJSON(ЗаписьJSON, Данные, НастройкиСериализации); 
    Возврат    ЗаписьJSON.Закрыть();    
КонецФункции // СериализоватьJSON()

Функция ДесериализоватьJSON(СтрокаJSON)
    ЧтениеJSON    = Новый ЧтениеJSON();
    ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
    Возврат ПрочитатьJSON(ЧтениеJSON);
КонецФункции // ДесериализоватьJSON()

#Область Данные_для_подключения
Функция ДанныеДляПодключения() Экспорт     
    // Подключаемся к сайту.
    Соединение = Новый HTTPСоединение(
        Константы.WSАдрес.Получить(), // сервер (хост)
        80, // порт, по умолчанию для http используется 80, для https 443
        Константы.WSПользователь.Получить(), // пользователь для доступа к серверу (если он есть)
        Константы.WSПароль.Получить(), // пароль для доступа к серверу (если он есть)
        , // здесь указывается прокси, если он есть
        30, // таймаут в секундах, 0 или пусто - не устанавливать
          // защищенное соединение, если используется https
    );
    Возврат Соединение;
КонецФункции // ДанныеДляПодключения()
#КонецОбласти

```



**ПРИЕМ HTTP СЕРВИСОМ** 

```
Функция НоменклатураПоКодуGET(Запрос)
Попытка
    УстановитьПривилегированныйРежим(Истина);
    НоменклатураКод    = Запрос.ПараметрыURL.Получить("code");        
    Ответ            = Новый HTTPСервисОтвет(200);
    Ответ.Заголовки.Вставить("Content-Type","application/json; charset=utf-8");
    Ответ.УстановитьТелоИзСтроки(МобильноеПриложение.НоменклатураПоНомеру(НоменклатураКод));
    Возврат Ответ;
Исключение
    Возврат МобильноеПриложение.ОтветОшибка400(ОписаниеОшибки());
КонецПопытки;
КонецФункции // НоменклатураПоКодуGET()

Функция CreateConsumptionPOST(Запрос)
Попытка
    УстановитьПривилегированныйРежим(Истина);
    МобильноеПриложение.СоздатьДокументРасхода(Запрос.ПолучитьТелоКакСтроку("UTF-8"));
    
    Ответ = Новый HTTPСервисОтвет(200);
    Ответ.Заголовки.Вставить("Content-Type","text/html; charset=utf-8");
    Ответ.УстановитьТелоИзСтроки("True");
    Возврат Ответ;
Исключение
    Возврат МобильноеПриложение.ОтветОшибка400(ОписаниеОшибки());
КонецПопытки;
КонецФункции // CreateConsumptionPOST()
```

