```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НайтиУникальныеРусскиеСловаВНоменклатуре();
	Отказ	= Истина;
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервереБезКонтекста
Процедура НайтиУникальныеРусскиеСловаВНоменклатуре()
	КЧ	= Новый КвалификаторыЧисла(5,0);
	КС	= Новый КвалификаторыСтроки(100);
	Массив	= Новый Массив;
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповС	= Новый ОписаниеТипов(Массив, , КС);
	Массив.Очистить();
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧ	= Новый ОписаниеТипов(Массив, , ,КЧ);
	РусскиеСлова	= Новый ТаблицаЗначений;	
	РусскиеСлова.Колонки.Добавить("Наименование",	ОписаниеТиповС,	"Наименование");
	РусскиеСлова.Колонки.Добавить("Количество",		ОписаниеТиповЧ,	"Количество");	
	
	// Выбираю всю номенклатуру ~3500 единиц 
	// (если номенклатуры нереально много - нужен другой подход)
	Запрос	= Новый Запрос("
	|ВЫБРАТЬ Наименование Слово
	|ИЗ Справочник.Номенклатура
	|ГДЕ Не ЭтоГруппа");
	Выборка	= Запрос.Выполнить().Выбрать();
	ПОКА Выборка.Следующий() ЦИКЛ
		Предложение	= СтрРазделить(Выборка.Слово, " ", Ложь);
		Для Каждого Слово Из Предложение Цикл
			Если ЭтоРусскоеСлово(Слово) Тогда
				нСтр	= РусскиеСлова.Добавить();
				нСтр.Наименование	= Слово;
				нСтр.Количество		= 1;
			КонецЕсли;
		КонецЦикла;
	КОНЕЦЦИКЛА; // Пока Выборка.Следующий()
	
	РусскиеСлова.Свернуть("Наименование", "Количество");
	РусскиеСлова.Сортировать("Количество Убыв");
	
	Файл	= Новый ЗаписьТекста("C:\УникальныеРусскиеСловаВНоменклатуре.csv");
	Файл.ЗаписатьСтроку("НАИМЕНОВАНИЕ;КОЛИЧЕСТВО");
	Для Каждого Строка Из РусскиеСлова Цикл
		Файл.ЗаписатьСтроку(СтрШаблон("%1;%2", 
										Строка.Наименование, 
										Формат(Строка.Количество, "ЧГ=0")));	
	КонецЦикла;	
	Файл.Закрыть();
КонецПроцедуры // НайтиУникальныеРусскиеСловаВНоменклатуре()

&НаСервереБезКонтекста
Функция ЭтоРусскоеСлово(Слово)
	Результат	= Ложь;
	Слово		= СтрЗаменить(Слово, "мм", "");
	МассивНаОчистку	= Новый Массив;
	Для Счетчик = 1 По СтрДлина(Слово) Цикл
		Символ	= КодСимвола(Слово, Счетчик);
		Если Символ > 1039 И Символ < 1104 Тогда
			Результат	= Истина;	
		ИначеЕсли Символ < 65 Или Символ > 122 Тогда
			МассивНаОчистку.Добавить(Символ(Символ));
		КонецЕсли;
	КонецЦикла;
	Если Результат Тогда
		Для Каждого Символ Из МассивНаОчистку Цикл
			Слово	= СтрЗаменить(Слово, Символ, "");	
		КонецЦикла;
	КонецЕсли;
	Возврат ?(СтрДлина(Слово)>1, Результат, Ложь);
КонецФункции // ЭтоРусскоеСлово()
```
