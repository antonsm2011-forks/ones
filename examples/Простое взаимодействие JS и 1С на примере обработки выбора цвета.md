# Простое взаимодействие JS и 1С на примере обработки выбора цвета.

Создал пример обработки 1с для продвинутого кроссплатформенного выбора цвета средствами JS. Обработка позволяет выбрать RGB цвет и вернуть его значение.

Вот код модуля:

```
#Область При_открытии

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СобратьHTML();	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура СобратьHTML()
	HTML		= "";
	УжеНайдено	= Ложь; 
	Обработка	= РеквизитФормыВЗначение("Объект");
	МакетHTML	= Обработка.ПолучитьМакет("HTML");
	
	// Заполняю массив стандартными либами
	МассивСкриптов	= Новый Массив;
	МассивСкриптов.Добавить(Обработка.ПолучитьМакет("raphael"));
	МассивСкриптов.Добавить(Обработка.ПолучитьМакет("colorpicker"));
	МассивСкриптов.Добавить(Обработка.ПолучитьМакет("colorwheel"));
	Обработка	= Неопределено;
	
	ДЛЯ Счетчик = 1 ПО МакетHTML.КоличествоСтрок() ЦИКЛ
		Строка	= МакетHTML.ПолучитьСтроку(Счетчик);		
		// Слегка ускоряю перебор, используя "ленивые" условия 1с
		Если Не УжеНайдено И СтрНайти(Строка, "<!--СКРИПТЫ-->") Тогда
			// Добавляю в HTML стандартные библиотеки raphael
			ДобавитьСкрипт(HTML, МассивСкриптов);
			// Прибираю за собой
			МассивСкриптов	= Неопределено;
			УжеНайдено		= Истина;
		Иначе
			// Добавляю в HTML тело
			HTML	= HTML + Символы.ПС + Строка;
		КонецЕсли;
	КОНЕЦЦИКЛА; // ДЛЯ Счетчик = 1 ПО МакетHTML.КоличествоСтрок()
КонецПроцедуры // СобратьHTML()

&НаСервере
Процедура ДобавитьСкрипт(HTML, Знач МассивСкриптов)
	Для Каждого Скрипт Из МассивСкриптов Цикл
		HTML	= HTML + Символы.ПС + "<script>";
		Для Счетчик = 1 ПО Скрипт.КоличествоСтрок() Цикл
			Строка	= Скрипт.ПолучитьСтроку(Счетчик);
			HTML	= HTML + Символы.ПС + Строка;		
		КонецЦикла;
		HTML	= HTML + Символы.ПС + "</script>";		
	КонецЦикла;
КонецПроцедуры // ДобавитьСкрипт()

#КонецОбласти


&НаКлиенте
Процедура ЗакрытьФорму(Команда)	
	ЭтаФорма.Закрыть(?(ТипЗнч(Команда) = Тип("КомандаФормы") И Команда.Имя = "Принять", 
						Элементы.HTML.Документ.getElementById("output").value, 
						Неопределено));
КонецПроцедуры // ЗакрытьФорму()
```

