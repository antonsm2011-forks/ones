# Обработка добавления фотографии в справочник «Файлы» на мобильном.

Мне потребовалось создавать с камеры смартфона фотографии и записывать в справочник файлов, для этого Я создал следующую обработку.

Работа с обработкой может происходить в двух плоскостях:

* При открытии формы, там есть возможность выбрать изображение из файловой системы через **ДиалогВыбораФайла()** или сделать снимок (для мобильных устройств) через **СредстваМультимедиа.СделатьФотоснимок()**.

```
// Можно передать вторым параметром наименование создаваемого 
// элемента справочника файлы, в противном случае оно будет установленно
// по умолчанию "Изображение"
Фото	= ОткрытьФормуМодально("Обработка.Фото.Форма", 
	  Новый Структура("Наименование", Объект.Наименование),
	  ЭтаФорма)
```



* При инициализации обработки как объекта и передачи Двоичных Данных в конструктор с последующим съемом значения.

```
//Обработка	= РеквизитФормыВЗначение("Объект");
Обработка	= Обработки.Фото.Создать()
ДД		= ПолучитьИзВременногоХранилища(Картинка);
Если Обработка.СоздатьФайлПоДвоичнымДанным(ДД, Наименование) Тогда
	Файл	= Обработка.ПолучитьФайл();
КонецЕсли;
```



* Модуль объекта обработки:

```
// Приватная переменная содержащая ссылку на справочник файлов
Перем Ф;

#Область Конструкторы

// Конструктор изображения элемента справочника "Файлы" по двоичным данным
// Возвращает Истина в случае успеха и Ложь в случае неудачи
// Параметры:
// ДД			- Двоичные данные изображения
// Наименование	- Наименование элемента справочника, если неопределено, то просто "Изображение"
// Расширение	- Расширение файла, если не передать - то найдеться оптимальное по сигнатурам файлов
Функция СоздатьФайлПоДвоичнымДанным(Знач ДД, Знач Наименование = Неопределено, Знач Расширение = Неопределено) Экспорт
	ОчиститьФайл();
	Наименование	= ?(Наименование = Неопределено или Наименование = "", "Изображение", Наименование);
	Размер			= ДД.Размер();

	// Отсеиваю слишком большие файлы
	Если Размер > 2097152 Тогда
		Сообщить(СтрШаблон("Слишком большой размер: %1 мб. Загрузите до 2 мегабайт.", Формат(Размер / 1048576, "ЧДЦ=2")));
		Возврат Ложь;
	КонецЕсли; 
	
	// Ищу расширение файла
	Расширение	= ?(Расширение = Неопределено, НайтиРасширениеПоСигнатуре(ДД), Расширение);
	Если Расширение = Неопределено Тогда
		Сообщить("Неизвестное расширение файла");
		Возврат Ложь;
	КонецЕсли;	
	
	// Создаю элемент справочника файлов
	Попытка
		нФайл				= Справочники.Файлы.СоздатьЭлемент();
		нФайл.Наименование	= Наименование;
		нФайл.Формат		= ПредопределенноеЗначение("Перечисление.ФорматыФайлов.Изображение");
		нФайл.Размер		= Размер;
		нФайл.Расширение	= Расширение;
		нФайл.ДатаСоздания	= ТекущаяДата();
		нФайл.ФайлХранилище	= Новый ХранилищеЗначения(ДД, Новый СжатиеДанных(9));
		нФайл.Записать();
		ф					= нФайл.Ссылка;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;		
	КонецПопытки;
	
	Возврат Истина;
КонецФункции // СоздатьФайлПоДвоичнымДанным()

#КонецОбласти


#Область Работа_с_файлом_объекта

// Функция возвращающая ссылку на ранее созданный файл объекта.
// Если ссылка не заполнена - возвращаю Неопределено.
Функция ПолучитьФайл() Экспорт
	Возврат ?(ЗначениеЗаполнено(Ф), Ф, Неопределено);        	
КонецФункции

// Процедура очищает файл
Процедура ОчиститьФайл() Экспорт
	Ф	= ПредопределенноеЗначение("Справочник.Файлы.ПустаяСсылка");	
КонецПроцедуры

#КонецОбласти



#Область Вспомогательные_функции

// Ищу расширение по сигнатуре первых байт двоичных данных
Функция НайтиРасширениеПоСигнатуре(Знач ДД)
	сРасширений	= СоответсвиеРасширенийИзображений();
	
	// Сколько максимум перебирать первых байт
	МаксРазмер	= 0;
	Для Каждого Расширение Из сРасширений Цикл
		МаксРазмер	= Макс(МаксРазмер, Расширение.Значение.Количество());		
	КонецЦикла;
	
	// Перебираю буфер файла начальных байт
	// и сравниваю с сигнатурами файлов
	Поток		= ДД.ОткрытьПотокДляЧтения();
	БуферФайла	= Новый БуферДвоичныхДанных(МаксРазмер);
	Поток.Прочитать(БуферФайла, 0, МаксРазмер);
	
	Для Каждого Расширение Из сРасширений Цикл
		Совпадение	= Истина;
		
		Для Счетчик = 0 По Расширение.Значение.Количество()-1 Цикл
			Если БуферФайла[Счетчик] <> Из_Любой_В_10(Расширение.Значение[Счетчик], 16) Тогда
				Совпадение	= Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Совпадение Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла; // Для Каждого Расширение Из сРасширений 		
	
	// Прибираюсь за собой
	Поток		= Неопределено;
	БуферФала	= Неопределено;
	
	// Интерпретирую результаты цикла
	Если Не Совпадение Тогда
		Возврат Неопределено;
	Иначе
		Возврат Расширение.Ключ;
	КонецЕсли;
КонецФункции // НайтиРасширениеПоСигнатуре()

// Возвращает соответсвие популярных расширений изображений (ключ)
// с их байт-сигнатурами (значение)
Функция СоответсвиеРасширенийИзображений()
	сРасширений	= Новый Соответствие;
	сРасширений.Вставить("PNG",	СтрРазделить("89 50 4E 47 0D 0A 1A 0A",	" ", Ложь));
	сРасширений.Вставить("JPG",	СтрРазделить("FF D8 FF",				" ", Ложь));
	сРасширений.Вставить("GIF",	СтрРазделить("47 49 46 38",				" ", Ложь));
	сРасширений.Вставить("BMP",	СтрРазделить("42 4D",					" ", Ложь));	
	Возврат сРасширений;	
КонецФункции // СоответсвиеРасширенийИзображений()

#КонецОбласти
```



* Для отображения картинки на форме размещаю реквизит формы с типом «**Строка**» и видом «**Поле картинки**«.

  А дальше наиболее удобным способом:

```
// 1 способ отображения:
Картинка	= ПоместитьВоВременноеХранилище(ДД, УникальныйИдентификатор);
// 2 способ отображения:
Картинка	= ПолучитьНавигационнуюСсылку(Объект.Файл, "ФайлХранилище");
```

Для более удобного использования возможно в будующем добавить возможность использовать вызовы интентов устройства.