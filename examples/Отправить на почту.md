# Отправить на почту

```

#Область Почта

// Получатели - массив с структурами и ключами "Имя" и "Почта" 
// или строка вида "Вася Числов:a@a.ru;Женя Строков:b@b.com;Маша Булева:c@c.ua"
Функция ОтправитьНаПочту(	Знач Получатели, 
							Знач Заголовок				= "", 
							Знач Текст					= "", 
							Знач ТипТекста				= Неопределено, // ТипТекстаПочтовогоСообщения 
							Знач МассивФайловыхПутей	= Неопределено,
							Знач МылоОтправителя		= Неопределено,	// В противном случае будет использоваться служебный емэйл
							Знач ИмяОтправителя			= Неопределено,	// В противном случае будет использоваться название текущей организации
							Знач КопиюСебе				= Ложь
							) Экспорт
	Попытка
		// Устанавливаю параметры по умолчанию
		Если Заголовок = "" И Текст = "" Тогда
			Возврат Ложь;
		ИначеЕсли Заголовок = "" Тогда
			Заголовок	= "Без темы";
		ИначеЕсли Текст = "" Тогда
			Текст		= Заголовок;	
		КонецЕсли; // Если Заголовок = "" И Текст = ""
		ТипТекста	= ?(ТипТекста = Неопределено, ТипТекстаПочтовогоСообщения.ПростойТекст, ТипТекста);
			
		Если МассивФайловыхПутей = Неопределено Тогда МассивФайловыхПутей = Новый Массив КонецЕсли;
				 
		Если ТипЗнч(Получатели) = Тип("Массив") И Получатели.Количество() Тогда
			МассивПолучателей	= Получатели;
		ИначеЕсли ТипЗнч(Получатели) = Тип("Строка") И Не ПустаяСтрока(Получатели) Тогда 
			МассивПолучателей	= Новый Массив;
			Для Каждого ИмяПочта Из СтрРазделить(Получатели, ";", Ложь) Цикл
				Если Найти(ИмяПочта, ":") Тогда
					МассивПолучателей.Добавить(Новый Структура("Имя,Почта", СокрЛП(Лев(ИмяПочта, Найти(ИмяПочта, ":")-1)), СокрЛП(Сред(ИмяПочта, Найти(ИмяПочта, ":")+1))));
				Иначе
					МассивПолучателей.Добавить(Новый Структура("Имя,Почта", СокрЛП(ИмяПочта), СокрЛП(ИмяПочта)));					
				КонецЕсли; // Если Найти(ИмяПочта, ":")
			КонецЦикла; // Для Каждого ИмяПочта Из СтрРазделить(Получатели, ";", Ложь)
		Иначе
			Возврат Ложь;			
		КонецЕсли; // Если ТипЗнч(Получатели) = Тип("Массив") И Получатели.Количество()
		Если КопиюСебе И Не ПустаяСтрока(МылоОтправителя) Тогда 
			МассивПолучателей.Добавить(Новый Структура("Имя,Почта", 
				СокрЛП(?(ПустаяСтрока(ИмяОтправителя),
					МылоОтправителя,
					ИмяОтправителя)), 
				СокрЛП(МылоОтправителя))) 
		КонецЕсли;
		
		АдресСервера		= Константы.СлужебнаяПочтаАдресSMTP.Получить();
		Порт				= Константы.СлужебнаяПочтаПортSMTP.Получить();
		EmailОтправителя	= Константы.СлужебнаяПочтаПользователь.Получить(); 
		ПарольОтправителя	= Константы.СлужебнаяПочтаПароль.Получить();
		
		//ПОЧТОВЫЙ ПРОФИЛЬ
		
		Профиль						= Новый ИнтернетПочтовыйПрофиль;
		Профиль.ИспользоватьSSLSMTP	= Ложь; //Истина;
		Профиль.АдресСервераSMTP	= АдресСервера; 
		Профиль.ПортSMTP			= Порт;
		Профиль.Пользователь		= EmailОтправителя;
		Профиль.Пароль				= ПарольОтправителя;
		Профиль.АутентификацияSMTP	= СпособSMTPАутентификации.Login;
		Профиль.ПользовательSMTP	= EmailОтправителя;
		Профиль.ПарольSMTP			= ПарольОтправителя;
		Профиль.ТолькоЗащищеннаяАутентификацияSMTP	= Истина;
	   
		//ПОЧТОВОЕ СООБЩЕНИЕ
		
		Сообщение				= Новый ИнтернетПочтовоеСообщение;
		Сообщение.Организация	= Строка(Константы.ТекущаяОрганизация.Получить());
		Сообщение.ИмяОтправителя= ?(ИмяОтправителя=Неопределено, Сообщение.Организация, ИмяОтправителя);
		Сообщение.Отправитель	= ?(МылоОтправителя=Неопределено, EmailОтправителя, МылоОтправителя);
		Сообщение.Тема			= Заголовок;
		//Сообщение.Кодировка		= "UTF-8";
		Сообщение.Важность		= ВажностьИнтернетПочтовогоСообщения.Высокая;
		Сообщение.Тексты.Добавить(Текст, ТипТекста);
		Для Каждого Получатель Из МассивПолучателей Цикл
			Адрес					= Сообщение.Получатели.Добавить(Получатель.Почта);
			Адрес.ОтображаемоеИмя	= Получатель.Имя;
		КонецЦикла;
		
		Для каждого ПутьКФайлу Из МассивФайловыхПутей Цикл
			Сообщение.Вложения.Добавить(ПутьКФайлу);
		КонецЦикла; 
		
		// ПОЧТОВЫЙ СЕРВЕР
		
		Почта = Новый ИнтернетПочта();
		
		//Подключение к серверу
		Попытка
			Почта.Подключиться(Профиль);
		Исключение
			Сообщение	= "Ошибка при отправке письма: " + ОписаниеОшибки();
			Статус		= ПредопределенноеЗначение("Перечисление.СтатусыЗаписейЛога.ОшибкаОтправкиНаПочту");
			Логи.Запись(Сообщение,Статус,ТекущаяДата(),,,Истина);
			Сообщить(Сообщение);
			Возврат Ложь;
		КонецПопытки;
		
		//Отправка письма
		Попытка
			Почта.Послать(Сообщение);
		Исключение
			Почта.Отключиться();
			Сообщение	= "Ошибка при отправке письма: " + ОписаниеОшибки();
			Статус		= ПредопределенноеЗначение("Перечисление.СтатусыЗаписейЛога.ОшибкаОтправкиНаПочту");
			Логи.Запись(Сообщение,Статус,ТекущаяДата(),,,Истина);
			Сообщить(Сообщение);
			Возврат Ложь;
		КонецПопытки;	
		Почта.Отключиться();
		Почта	= Неопределено;
		Возврат Истина;
	Исключение	
		Сообщение	= "Ошибка при отправке письма: " + ОписаниеОшибки();
		Статус		= ПредопределенноеЗначение("Перечисление.СтатусыЗаписейЛога.ОшибкаОтправкиНаПочту");
		Логи.Запись(Сообщение,Статус,ТекущаяДата(),,,Истина);
		Сообщить(Сообщение);
		Возврат Ложь;
	КонецПопытки;
КонецФункции // ОтправитьНаПочту()

#КонецОбласти

```

