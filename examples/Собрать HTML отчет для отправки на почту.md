# Собрать HTML отчет для отправки на почту

```
// Заявки на оплату, список на утверждение
Функция ОтчетЗаявкиНаОплатуСписокНаУтверждениеHTML(Знач ВыборкаОтделы = Неопределено, Знач Комментарий = "") Экспорт
	Отчет	= "<!DOCTYPE HTML>
	|<HTML lang='ru-RU'>
	|<HEAD>
	|    <META HTTP-EQUIV='X-UA-Compatible' CONTENT='text/html;IE=11'>
	|    <meta charset='utf-8'>
	|    <style type='text/css'>
	|        body {background-color:white;}
	|        table {font-size:12px; color:black; width:100%; border-width:0px;}
	|        th {font-size:12px; background-color:lightblue; border-width:1px; padding: 8px; border-style:solid; border-color:gray;}
	|        td {background-color:lightskyblue; font-size:12px; border-width:1px; padding: 8px; border-style:solid; border-color:gray; white-space:pre-wrap;}
	|        td:nth-of-type(n+4), td:nth-of-type(2), th:nth-of-type(n+2) {text-align: right;}
	|        td:nth-of-type(3), th:nth-of-type(1) {text-align: left;}
	|        tr:not(.header) {background-color:lightcyan;}
	|        tr:not(.header):hover {background-color:black; color:orange}
	|        .empty-cell {empty-cells:hide; background-color:white; border-width:0px; border-style:none; border-color:white;}
	|        p { white-space: pre-wrap;			/* Переносы */ }
	|    </style>
	|</HEAD>
	|    <BODY>
	|    <h1 align='center'><!--ЗАГОЛОВОК--></h1>
	|    </br>
	|    <!--ОТДЕЛЫ-->
	|    </BODY>
	|</HTML>
	|";                  
	ОтделМакет	= "	
	|    <h3><b>%1</b></h3>
	|    <table cols='5' border='0' width='100^' cellpadding='5'>
	|        <col width='1^' valign='middle' align='left'>
	|        <col width='14^' valign='middle' align='right'>
	|        <col width='55^' valign='middle' align='left'>
	|        <col width='10^' valign='middle' align='right'>
	|        <col width='20^' valign='middle' align='right'>
	|        %2
	|        <tr class='header'><th class='empty-cell' colspan='4'><th valign='middle' align='right' width='20^'>%3</th></tr>
	|    </table>
	|    ";
	КонтрагентМакет	= "
	|        <tr class='header'><th colspan='4' valign='middle' align='left'><b>%1</b></th><th valign='middle' align='right' width='20^'><b>%2</b></th></tr>
	|            %3
	|    ";
	ДанныеМакет	= "
	|            <tr><td class='empty-cell'></td><td width='10^' valign='middle' align='right' bgcolor='lightskyblue'>%1</td><td width='55^' valign='middle' align='left' bgcolor='lightskyblue'>%2</td><td width='10^' valign='middle' align='right' bgcolor='lightskyblue'>%3</td><td width='20^' valign='middle' align='right' bgcolor='lightskyblue'>%4</td></tr>
	|            ";	
	Запрос	= Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(Регистратор.Ответственный.Отдел.Наименование, 1, 9) = ""Снабжение""	
	|			ТОГДА ""1. Снабжение""
	|		КОГДА ПОДСТРОКА(Регистратор.Ответственный.Отдел.Наименование, 1, 9) = ""Инженерно""	
	|			ТОГДА ""2. Инженерно техническая служба""
	|		ИНАЧЕ ""3. Прочие""
	|	КОНЕЦ	КАК Отдел,
	|	ВЫБОР
	|		КОГДА Контрагент.НаименованиеСокращенное = """"
	|			ТОГДА Контрагент.Наименование
	|		ИНАЧЕ Контрагент.НаименованиеСокращенное
	|	КОНЕЦ											КАК Контрагент,
	|	ТЧ.ОжидаемаяДатаОплаты							КАК ОплатитьДо,
	|	Регистратор.Описание							КАК Описание,
	|	Представление(ТЧ.ПроцентОплаты)					КАК ПроцентОплаты,
	|	ВЫБОР
	|		КОГДА Регистратор.Валюта = Значение(Справочник.Валюты.RUB)
	|			ТОГДА ТЧ.Сумма
	|		КОГДА КурсВалют.Курс IS NULL
	|			ТОГДА 9999999999.99 
	|		ИНАЧЕ ТЧ.Сумма * КурсВалют.Курс
	|	КОНЕЦ											КАК Сумма 
	|ИЗ
	|	РегистрСведений.РегистрЗаявокНаОплату КАК Рег
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкиНаОплату.РазбитиеПлатежей КАК ТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсВалют.СрезПоследних(&ТекущаяДата) КАК КурсВалют
	|			ПО КурсВалют.Валюта = ТЧ.Ссылка.Валюта	
	|		ПО (Рег.Регистратор = ТЧ.Ссылка)
	|			И (Рег.Номер = ТЧ.НомерСтроки)
	|ГДЕ
	|	Не ПодтверждениеОплаты
	|	И ВыставитьОплату
	|	И ТЧ.ОжидаемаяДатаОплаты < ДОБАВИТЬКДАТЕ(&ТекущаяДата, ДЕНЬ, 2)
	|УПОРЯДОЧИТЬ ПО
	|	Отдел ВОЗР, Сумма УБЫВ
	|АВТОУПОРЯДОЧИВАНИЕ
	|ИТОГИ
	|	Сумма(Сумма)
	|ПО
	|	Отдел, Контрагент");
	Запрос.УстановитьПараметр("ТекущаяДата",	ТекущаяДата());		
	Попытка
		ВыборкаОтделы	= ?(ВыборкаОтделы=Неопределено, Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам), ВыборкаОтделы);
		// Заполняю заголовок
		ОтчетЗаголовок	= СтрШаблон("Заявки на оплату, список на утверждение от %1 г.", Формат(ТекущаяДата(), "ДФ='d MMM yyyy'"));
		Отчет			= СтрЗаменить(Отчет, "<!--ЗАГОЛОВОК-->", ОтчетЗаголовок);
		Отделы			= "";
		Пока ВыборкаОтделы.Следующий() Цикл
			ВыборкаКонтрагенты	= ВыборкаОтделы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Контрагенты			= "";
			Пока ВыборкаКонтрагенты.Следующий() Цикл
				ВыборкаДанные	= ВыборкаКонтрагенты.Выбрать();
				Данные			= "";
				Пока ВыборкаДанные.Следующий() Цикл
					Данные		= Данные + СтрШаблон(ДанныеМакет,
									Формат(ВыборкаДанные.ОплатитьДо, "ДФ='d MMM'"), 
									Строка(ВыборкаДанные.Описание),
									"" + Окр(ВыборкаДанные.ПроцентОплаты, 2) + "^",
									Формат(Окр(ВыборкаДанные.Сумма, 2), "ЧДЦ=2; ЧРД=,; ЧН=0; ЧГ=3,0") + " &#8381;");				
				КонецЦикла; // Пока ВыборкаДанные.Следующий()
				Контрагенты		= Контрагенты + СтрШаблон(КонтрагентМакет,
									ВыборкаКонтрагенты.Контрагент,
									Формат(Окр(ВыборкаКонтрагенты.Сумма, 2), "ЧДЦ=2; ЧРД=,; ЧН=0; ЧГ=3,0") + " &#8381;",
									Данные);
			КонецЦикла;	// Пока ВыборкаКонтрагенты.Следующий()
			Отделы				= Отделы + СтрШаблон(ОтделМакет,
									Сред(ВыборкаОтделы.Отдел, 4),
									Контрагенты,
									Формат(Окр(ВыборкаОтделы.Сумма, 2), "ЧДЦ=2; ЧРД=,; ЧН=0; ЧГ=3,0") + " &#8381;"); 					
		КонецЦикла; // Пока ВыборкаОтделы.Следующий()
		// Заполняю отделы
		Отделы			= Отделы + "
							|</br></br>"+Комментарий+"</br>";
		Отчет			= СтрЗаменить(Отчет, "<!--ОТДЕЛЫ-->", Отделы);
		Отчет			= СтрЗаменить(Отчет, "^", "%");
	Исключение
		ОтчетЗаголовок	= СтрШаблон("Ошибка при формировании отчета: Заявки на оплату, список на утверждение от %1 г.", Формат(ТекущаяДата(), "ДФ='d MMM yyyy'"));
		Отчет			= ОписаниеОшибки();		
	КонецПопытки;
	Возврат Новый Структура("Заголовок,Текст", ОтчетЗаголовок, Отчет);
КонецФункции // ОтчетЗаявкиНаОплатуСписокНаУтверждениеHTML()

```

