```
//В модуле объекта
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь; 

	Настройки = КомпоновщикНастроек.Настройки;

	//Удалям созданные нами предустановленные условные оформления
	//для того, чтобы не дублировать их при повторном формировании отчета
	СписокУдаляемыхЭлементов = Новый СписокЗначений;
	Для каждого ЭлементУсловногоОформления Из Настройки.УсловноеОформление.Элементы Цикл
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный" Тогда
			СписокУдаляемыхЭлементов.Добавить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	Для каждого Элемент Из СписокУдаляемыхЭлементов Цикл
		Настройки.УсловноеОформление.Элементы.Удалить(Элемент.Значение);
	КонецЦикла;
	
	//Обхожу все ГП, что есть в отчете и устанавливаю условное оформление по цвету фона

	Если Строка(Настройки.ПараметрыДанных.Элементы[2].Значение) = "" или Строка(Настройки.ПараметрыДанных.Элементы[2].Значение) = "ГП" Тогда
		НачалоПериода	= Настройки.ПараметрыДанных.Элементы[0].Значение;
		КонецПериода	= Настройки.ПараметрыДанных.Элементы[1].Значение;
		
		Запрос	= Новый Запрос(
		"ВЫБРАТЬ
		|	Ост.Номенклатура				КАК Номенклатура,
		|	Ост.Номенклатура.Цвет.Красный	КАК Красный,
		|	Ост.Номенклатура.Цвет.Зеленый	КАК Зеленый,
		|	Ост.Номенклатура.Цвет.Синий		КАК Синий
		|ИЗ
		|	РегистрНакопления.ОстаткиПоСкладу.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , ) КАК Ост
		|ГДЕ
		|	Ост.Номенклатура.Вид = Значение(Перечисление.ВидыНоменклатур.ГП)"
		);
		Запрос.Параметры.Вставить("НачалоПериода",НачалоПериода);
		Запрос.Параметры.Вставить("КонецПериода",КонецПериода);
		Выборка	= Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл

			ЭлементУсловногоОформления	= Настройки.УсловноеОформление.Элементы.Добавить();;
			ОформляемоеПоле					= ЭлементУсловногоОформления.Поля.Элементы.Добавить();
			ОформляемоеПоле.Поле			= Новый ПолеКомпоновкиДанных("ПолеДляОформленияЦветом");

			ЭлементОтбора	= ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));

			ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Номенклатура"); 
			ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение	= Выборка.Номенклатура; 

			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(Выборка.Красный,Выборка.Зеленый,Выборка.Синий));
			ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			//Указываем идентификатор настройки для того, чтобы при повторном формировании отчета
			//удалить все элементы с этим идентификатором
			ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
		КонецЦикла;		
	КонецЕсли;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки,ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
КонецПроцедуры
```