# Сформируем список присутсвующих сотрудников с ссылками в HTML поле

```
#Область СписокПрисутствующихСотрудников

&НаСервереБезКонтекста
Процедура СписокПрисутствующихСотрудников(HTML)
	Сегодня		= ТекущаяДата();
	Вчера		= Сегодня-86400;
	ДеньСегодня	= День(Сегодня);
	ДеньВчера	= День(Вчера);
		
	Запрос	= Новый  Запрос("
	|// Выбираем дневную смену
	|ВЫБРАТЬ
	|	""Дневная""			КАК Смена,  
	|	С.Группа			КАК Отдел, 
	|	С.Ссылка			КАК Сотрудник, 
	|	Минимум(Р.Время)	КАК Время
	|ПОМЕСТИТЬ втСмены
	|ИЗ 
	|	Справочник.Сотрудники	КАК С
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ГрафикПроизводства.ГрафикиПроизводства КАК Д
	|		ПО С.График = Д.НомерГрафика
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроходыСигур	КАК Р
	|		ПО С.Ссылка = Р.Сотрудник И Р.Период Между НачалоПериода(&ДатаДневнаяСмена, День) И КонецПериода(&ДатаДневнаяСмена, День)
	|ГДЕ 
	|	С.Статус = Значение(Справочник.СотрудникиСтатус.Работает) И Не С.ПометкаУдаления 
	|	И Д.Ссылка.Дата МЕЖДУ НачалоПериода(&ДатаДневнаяСмена, Месяц) И КонецПериода(&ДатаДневнаяСмена, Месяц) 
	|	И Д.Ч"+ДеньСегодня+" = ""д""
	|	И Группа <> Значение(Перечисление.ГруппыСотрудников.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	Группа, С.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Выбираем ночную смену
	|ВЫБРАТЬ
	|	""Ночная"", 
	|	С.Группа, 
	|	С.Ссылка, 
	|	Минимум(Р.Время)
	|ИЗ 
	|	Справочник.Сотрудники	КАК С
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ГрафикПроизводства.ГрафикиПроизводства КАК Д
	|		ПО С.График = Д.НомерГрафика
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроходыСигур	КАК Р
	|		ПО С.Ссылка = Р.Сотрудник
	|		И Р.Период Между НачалоПериода(&ДатаНочнаяСмена, День) И КонецПериода(&ДатаНочнаяСмена, День)
	|		И Р.Время Между ДобавитьКДате(ДатаВремя(1,1,1), Час, 17) И КонецПериода(ДатаВремя(1,1,1), День)
	|ГДЕ 
	|	С.Статус = Значение(Справочник.СотрудникиСтатус.Работает) И Не С.ПометкаУдаления 
	|	И Д.Ссылка.Дата МЕЖДУ НачалоПериода(&ДатаНочнаяСмена, Месяц) И КонецПериода(&ДатаНочнаяСмена, Месяц) 
	|	И Д.Ч"+ДеньВчера+" = ""н""
	|	И Группа <> Значение(Перечисление.ГруппыСотрудников.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	Группа, С.Ссылка
	|;
	|ВЫБРАТЬ
	|	Смена,
	|	Отдел, 
	|	Сотрудник, 
	|	ЕстьNULL(Время, ""Отсутсвие"")	КАК Время
	|ИЗ втСмены
	|УПОРЯДОЧИТЬ ПО
	|	Смена УБЫВ,
	|	Отдел.Порядок ВОЗР
	|ИТОГИ
	|	Количество(Сотрудник),
	|	Минимум(Время)
	|ПО
	|	Смена, Отдел");
	Запрос.УстановитьПараметр("ДатаДневнаяСмена", Сегодня);
	Запрос.УстановитьПараметр("ДатаНочнаяСмена", Вчера);
	ВыборкаСмена	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	//ЗначениеВРеквизитФормы(Результат, "ДеревоЗначений");
	
	// Собираем HTML	
	Текст	= "<!DOCTYPE html>
	|<HTML lang='ru-RU'>
	|<HEAD>
	|    <TITLE>Смены</TITLE>
	|    <META charset='utf-8'>
	|    <META http-equiv='X-UA-Compatible' content='IE=11'>
	|    <STYLE type=text/css>
	|        BODY {
	|			margin: 2px;
	|        }
	|        a, a:hover { 
	|			text-decoration: none; /* Отменяем подчеркивание у ссылки */
	|			color: inherit;
	|        }
	|        div#Смены {
	|            margin: 0;
	|        }
	|        h3 {
	|            margin: 5px 0 5px 0;
	|            display: inline-block;
	|            padding: 0;
	|            FONT: 12px 'Segoe UI Semibold', serif;
	|            COLOR: #993300;
	|        }
	|        table {
	|            width: 95%;
	|            margin: 0 0 2px 5%;
	|            padding: 0;
	|            word-wrap: break-word;
	|            border-collapse: collapse;
	|            FONT: 12px 'Consolas';
	|        }
	|        div.БлокСмены {
	|            padding: 5px;
	|            border-radius: 15px;
	|        }
	|        div#Дневная {
	|            background: #ffffbf;
	|        }
	|        div#Ночная {
	|            background: #bfbfff;
	|        }
	|        th.Сотрудник {
	|        }
	|        th.Время {
	|            width: 70px;
	|        }
	|        td {
	|            white-space: pre-wrap;
	|            word-wrap: break-word;
	|            word-break: break-all;
	|            vertical-align: middle;
	|            border-bottom: 1px solid black;
	|        }
	|        tr.Прогул {
	|            background: red;
	|        }
	|    </STYLE>
	|</HEAD>
	|<BODY>
	|    <div id='Смены'>
	|";
	
	Пока ВыборкаСмена.Следующий() Цикл
		Текст	= Текст + "<div id='" + ВыборкаСмена.Смена + "' class='БлокСмены'>";		
		ВыборкаОтдел	= ВыборкаСмена.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОтдел.Следующий() Цикл
            Текст	= Текст + "
			|<h3>" + ВыборкаОтдел.Отдел +"</h3>			
			|<table>
			|    <thead>
			|    <tr>
			|        <th class='Сотрудник'></th>
			|        <th class='Время'></th>
			|    </tr>
			|    </thead>
			|    <tbody>";			
			Выборка	= ВыборкаОтдел.Выбрать();
			Пока Выборка.Следующий() Цикл
				Текст	= Текст + СтрШаблон("
				|<tr %1>
				|    <td><A href= v8:%2 >%3</A></td>
				|    <td align='right'>%4</td>
				|</tr>", ?(Выборка.Время="Отсутсвие", "class='Прогул'", ""), ЗначениеВСтрокуВнутр(Выборка.Сотрудник), Выборка.Сотрудник, Формат(Выборка.Время, "ДФ=HH:mm"));				
			КонецЦикла; // Пока Выборка.Следующий()
			Текст	= Текст + "
			|    </tbody>
			|</table>";
		КонецЦикла; // Пока ВыборкаОтдел.Следующий()		
		Текст	= Текст + "</div>";
	КонецЦикла; // Пока ВыборкаСмена.Следующий()	
	Текст	= Текст + "
	|    </div>
	|</BODY>
	|</HTML>";	
	HTML	= Текст;
КонецПроцедуры // СписокПрисутствующихСотрудников() 

&НаКлиенте
Процедура СписокПрисутствующихСотрудниковПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Если ДанныеСобытия.Anchor = Неопределено Или ВРег(ДанныеСобытия.Anchor.tagName) <> "A" Тогда Возврат КонецЕсли;
	СтандартнаяОбработка	= Ложь;
	Ссылка	= ПолучитьСсылкуСотрудникаИзСтрокиВнутр(Сред(ДанныеСобытия.Href, 4));
	Если Ссылка.Пустая() Тогда Сообщить("Ошибка получения ссылки сотрудника."); Возврат КонецЕсли;
	ОткрытьЗначение(Ссылка);
КонецПроцедуры // СписокПрисутствующихСотрудниковПриНажатии()

&НаСервереБезКонтекста
Функция ПолучитьСсылкуСотрудникаИзСтрокиВнутр(Строка)
	Попытка 
		Ссылка	= ЗначениеИзСтрокиВнутр(Строка);
		Если Ссылка.ПолучитьОбъект() = Неопределено Тогда 
			Возврат ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
		Иначе
			Возврат Ссылка;
		КонецЕсли;
	Исключение 
		Возврат ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");		
	КонецПопытки;
КонецФункции // ПолучитьСсылкуСотрудникаИзСтрокиВнутр()

#КонецОбласти
```

