```

#Область Основная_информация_об_обработке
// <-- ОСНОВНАЯ ИНФОРМАЦИЯ НАЧАЛО 
Функция ПолучитьИнформациюОбОбработке()
	Инф	= Новый Структура;
	// Наименование обработки
	Инф.Вставить("Наименование",	"Передача сырья в переработку (МХ-3)");
	// Идентификатор без пробелов и знаков препинания
	Инф.Вставить("Идентификатор",	"ПередачаСырьяВПереработку");				
	// Дополнительная информация
	Инф.Вставить("Информация",		"Отчет МХ-3 передачи сырья в переработку");	
	// Указываем документ к которому делаем внешнюю печ. форму
	Инф.Вставить("Назначение",		"Документ.ПередачаТоваров");			

	Возврат Инф;	
КонецФункции // ПолучитьИнформациюОбОбработке()
// ОСНОВНАЯ ИНФОРМАЦИЯ КОНЕЦ -->
#КонецОбласти

#Область Печать
// <-- ПЕЧАТЬ НАЧАЛО 
Функция СформироватьПечатнуюФорму(Ссылка, ОбъектыПечати)
	Инф							= ПолучитьИнформациюОбОбработке();
	ТабДок						= Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_" + Инф.Идентификатор;
	МакетОбработки				= ПолучитьМакет("Макет");
	оШапка						= МакетОбработки.ПолучитьОбласть("Шапка");
	оДанные						= МакетОбработки.ПолучитьОбласть("Данные");
	оПодвал						= МакетОбработки.ПолучитьОбласть("Подвал");
	
	// Заполняем шапку
	оШапка.Параметры.КонтрагентЮрАдрес		= ПредставлениеОрганизацииКонтрагента(Ссылка.Контрагент,	"Юридический адрес");
	оШапка.Параметры.КонтрагентФактАдрес	= ПредставлениеОрганизацииКонтрагента(Ссылка.Контрагент,	"Фактический адрес");
	оШапка.Параметры.ОрганизацияЮрАдрес		= ПредставлениеОрганизацииКонтрагента(Ссылка.Организация,	"Юридический адрес");
	
	оШапка.Параметры.КонтрагентКодПоОКПО	= Ссылка.Контрагент.КодПоОКПО;
	оШапка.Параметры.ОрганизацияКодПоОКПО	= Ссылка.Организация.КодПоОКПО;
	
	оШапка.Параметры.НомерДокумента			= Ссылка.Номер;
	оШапка.Параметры.ДатаДокумента			= Ссылка.Дата;
	
	оШапка.Параметры.ДоговорНомер			= Ссылка.ДоговорКонтрагента.Номер;
	оШапка.Параметры.ДоговорДата			= Ссылка.ДоговорКонтрагента.Дата;
	
	// Выводим шапку в табличный документ
	ТабДок.Вывести(оШапка);

	// Заполняем строки ТЧ
	Для Каждого ТекСтр Из Ссылка.Товары Цикл
		ЗаполнитьЗначенияСвойств(оДанные.Параметры, ТекСтр);
		оДанные.Параметры.ЕдИзм		= ТекСтр.Номенклатура.ЕдиницаИзмерения;
		оДанные.Параметры.КодПоОКЕИ	= ТекСтр.Номенклатура.ЕдиницаИзмерения.Код;
		ТабДок.Вывести(оДанные);
	КонецЦикла;

	// Заполняем подвал
	//оПодвал.Параметры.КоличествоИтог		= Ссылка.Товары.Итог("Количество");
	
	// Выводим подвал в табличный документ
	ТабДок.Вывести(оПодвал);

	ТабДок.АвтоМасштаб			= Истина;
	ТабДок.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДок.ПолеСлева			= 5;
	ТабДок.ПолеСправа			= 5;

	Возврат ТабДок;
КонецФункции // СформироватьПечатнуюФорму()
// ПЕЧАТЬ КОНЕЦ -->
#КонецОбласти

#Область Подготовка_к_регистрации_и_печати
// <-- ПОДГОТОВКА РЕГИСТРАЦИИ ГЛОБАЛЬНОГО ОТЧЕТА НАЧАЛО
Функция СведенияОВнешнейОбработке() Экспорт 
	Инф						= ПолучитьИнформациюОбОбработке();		

	МассивНазначений		= Новый Массив;
	ТаблицаКоманд			= ПолучитьТаблицуКоманд();	
	ПараметрыРегистрации	= Новый Структура;
	МассивНазначений.Добавить(Инф.Назначение);
	ПараметрыРегистрации.Вставить("Назначение",			МассивНазначений);
	ПараметрыРегистрации.Вставить("Вид",				"ПечатнаяФорма");	// Варианты: "ДополнительнаяОбработка", "ДополнительныйОтчет", "ЗаполнениеОбъекта", "Отчет", "ПечатнаяФорма", "СозданиеСвязанныхОбъектов" 
	ПараметрыРегистрации.Вставить("Наименование",		Инф.Наименование); 
	ПараметрыРегистрации.Вставить("Версия",				"1.0");				// "1.0" 
	ПараметрыРегистрации.Вставить("БезопасныйРежим",	Ложь);				// Варианты: Истина, Ложь 
	ПараметрыРегистрации.Вставить("Информация",			Инф.Информация); 
	ПараметрыРегистрации.Вставить("Команды",			ТаблицаКоманд); 

	ДобавитьКоманду(ТаблицаКоманд, 
	Инф.Наименование, 
	Инф.Идентификатор, 
	"ВызовСерверногоМетода",	// Использование. Варианты: "ОткрытиеФормы", "ВызовКлиентскогоМетода", "ВызовСерверногоМетода"  
	Ложь,						// Показывать оповещение. Варианты Истина, Ложь 
	"ПечатьMXL");				// Модификатор 

	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд); 

	Возврат ПараметрыРегистрации;
КонецФункции // СведенияОВнешнейОбработке() 

Функция ПолучитьТаблицуКоманд()
	Команды	= Новый ТаблицаЗначений; 
	Команды.Колонки.Добавить("Представление",			Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("Идентификатор",			Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("Использование",			Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("ПоказыватьОповещение",	Новый ОписаниеТипов("Булево")); 
	Команды.Колонки.Добавить("Модификатор",				Новый ОписаниеТипов("Строка")); 

	Возврат Команды; 
КонецФункции // ПолучитьТаблицуКоманд() 

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	НоваяКоманда						= ТаблицаКоманд.Добавить(); 
	НоваяКоманда.Представление			= Представление; 
	НоваяКоманда.Идентификатор			= Идентификатор; 
	НоваяКоманда.Использование			= Использование; 
	НоваяКоманда.ПоказыватьОповещение	= ПоказыватьОповещение; 
	НоваяКоманда.Модификатор			= Модификатор;
КонецПроцедуры // ДобавитьКоманду()

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	Инф	= ПолучитьИнформациюОбОбработке();	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
	КоллекцияПечатныхФорм, 
	Инф.Идентификатор,	// Идентификатор 
	Инф.Наименование,	// Наименование 
	СформироватьПечатнуюФорму(МассивОбъектов[0], ОбъектыПечати));
КонецПроцедуры // Печать()
// ПОДГОТОВКА РЕГИСТРАЦИИ ГЛОБАЛЬНОГО ОТЧЕТА КОНЕЦ -->
#КонецОбласти

#Область Полезные_функции
// <-- ПОЛЕЗНЫЕ ФУНКЦИИ НАЧАЛО  

// Контрагент	- ссылка на организацию или контрагент
// Вид			- "Юридический адрес", "Фактический адрес", "Почтовый адрес"
Функция ПредставлениеОрганизацииКонтрагента(Знач Контрагент, Знач Вид = "Юридический адрес")
	Представление	= "";
	Запрос			= Новый Запрос("
	|ВЫБРАТЬ Ссылка.НаименованиеПолное + "", "" + Представление
	|КАК Представление
	|ИЗ Справочник.Организации.КонтактнаяИнформация
	|ГДЕ Ссылка = &Контрагент И Вид.Наименование = &Вид
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ Ссылка.НаименованиеПолное + "", "" + Представление 
	|ИЗ Справочник.Контрагенты.КонтактнаяИнформация
	|ГДЕ Ссылка = &Контрагент И Вид.Наименование = &Вид");
	Запрос.УстановитьПараметр("Контрагент",	Контрагент);
	Запрос.УстановитьПараметр("Вид",		Вид);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Представление	= Выборка.Представление;	
	КонецЦикла;
	Возврат Представление;	
КонецФункции // ПредставлениеОрганизацииКонтрагента()
// ПОЛЕЗНЫЕ ФУНКЦИИ КОНЕЦ -->
#КонецОбласти
```